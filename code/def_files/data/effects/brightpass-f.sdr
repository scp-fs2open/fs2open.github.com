in vec4 fragTexCoord;
out vec4 fragOut0;
uniform sampler2D tex;

// Soft knee bloom threshold parameters
const float threshold = 1.0;
const float knee = 0.5;

// Attempt to match ITU BT.709 luminance coefficients
float luminance(vec3 color)
{
	return dot(color, vec3(0.2126, 0.7152, 0.0722));
}

void main() {
	vec4 color = texture(tex, fragTexCoord.xy);
	float brightness = luminance(color.rgb);

	// Soft knee threshold - provides gradual transition instead of hard cutoff
	// This produces more natural-looking bloom without harsh edges
	float soft = brightness - threshold + knee;
	soft = clamp(soft, 0.0, 2.0 * knee);
	soft = soft * soft / (4.0 * knee + 0.00001);

	float contribution = max(soft, brightness - threshold);
	contribution /= max(brightness, 0.00001);

	fragOut0 = vec4(color.rgb * contribution, 1.0);
}
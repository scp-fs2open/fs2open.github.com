in vec4 vertPosition;
in vec4 vertTexCoord;
in vec3 vertNormal;
in vec4 vertTangent;
in float vertModelID;
uniform mat4 modelViewMatrix;
uniform mat4 modelMatrix;
uniform mat4 viewMatrix;
uniform mat4 projMatrix;
uniform mat4 textureMatrix;
uniform vec4 color;
#ifdef FLAG_NORMAL_EXTRUDE
uniform float extrudeWidth;
#endif
#ifdef FLAG_ENV_MAP
uniform mat4 envMatrix;
out vec3 fragEnvReflect;
#endif

uniform bool FLAG_NORMAL_MAP;

#ifndef HAS_GEOMETRY_SHADER
out mat3 fragTangentMatrix;
#endif

#ifdef FLAG_FOG
uniform float fogStart;
uniform float fogScale;
out float fragFogDist;
#endif
#ifdef FLAG_TRANSFORM
uniform samplerBuffer transform_tex;
uniform int buffer_matrix_offset;
#ifdef FLAG_SHADOW_MAP
out float geoNotVisible;
#else
out float fragNotVisible;
#endif
#endif
#ifdef FLAG_SHADOW_MAP
out float geoInstance;
uniform mat4 shadow_proj_matrix[4];
out vec3 geoNormal;
out vec4 geoTexCoord;
#else
out vec4 fragPosition;
out vec3 fragNormal;
out vec4 fragTexCoord;
#endif
#ifdef FLAG_SHADOWS
uniform mat4 shadow_mv_matrix;
uniform mat4 shadow_proj_matrix[4];
out vec4 fragShadowUV[4];
out vec4 fragShadowPos;
#endif
#ifdef FLAG_THRUSTER
uniform float thruster_scale;
#endif
#ifdef FLAG_CLIP
uniform int use_clip_plane;
uniform vec3 clip_normal;
uniform vec3 clip_position;
#ifdef FLAG_SHADOW_MAP
out float geoClipDistance;
#else
out float fragClipDistance;
#endif
#endif
#ifdef FLAG_TRANSFORM
#define TEXELS_PER_MATRIX 4
void getModelTransform(inout mat4 transform, out float invisible, int id, int matrix_offset)
{
	transform[0] = texelFetch(transform_tex, (matrix_offset + id) * TEXELS_PER_MATRIX);
	transform[1] = texelFetch(transform_tex, (matrix_offset + id) * TEXELS_PER_MATRIX + 1);
	transform[2] = texelFetch(transform_tex, (matrix_offset + id) * TEXELS_PER_MATRIX + 2);
	transform[3] = texelFetch(transform_tex, (matrix_offset + id) * TEXELS_PER_MATRIX + 3);
	invisible = transform[3].w;
	transform[3].w = 1.0;
}
#endif
#ifdef FLAG_SHADOWS
vec4 transformToShadowMap(int i, vec4 pos)
{
	vec4 shadow_proj;
	shadow_proj = shadow_proj_matrix[i] * pos;
	shadow_proj += 1.0;
	shadow_proj *= 0.5;
	shadow_proj.w = shadow_proj.z;
	shadow_proj.z = float(i);
	return shadow_proj;
}
#endif
void main()
{
	vec4 position;
	vec3 normal;
	vec4 texCoord;
	mat4 orient = mat4(1.0);
	mat4 scale = mat4(1.0);
 #ifdef FLAG_TRANSFORM
	float invisible;
	getModelTransform(orient, invisible, int(vertModelID), buffer_matrix_offset);
  #ifdef FLAG_SHADOW_MAP
	geoNotVisible = invisible;
  #else
	fragNotVisible = invisible;
  #endif
 #endif
	texCoord = textureMatrix * vertTexCoord;
	vec4 vertex = vertPosition;
 #ifdef FLAG_THRUSTER
	if(vertex.z < -1.5) {
		vertex.z *= thruster_scale;
	}
 #endif
 // Transform the normal into eye space and normalize the result.
	normal = normalize(mat3(modelViewMatrix) * mat3(orient) * vertNormal);
	position = modelViewMatrix * orient * vertex;
 #ifdef FLAG_SHADOW_MAP
	gl_Position = position;
  #ifdef APPLE
	 geoInstance = float(gl_InstanceIDARB);
  #else
	 geoInstance = float(gl_InstanceID);
  #endif
 #else
	gl_Position = projMatrix * position;
 #endif
 #ifdef FLAG_NORMAL_EXTRUDE
	gl_Position.xy += (mat3(projMatrix) * normal.xyz).xy * gl_Position.z * extrudeWidth;
 #endif
 #ifdef FLAG_SHADOWS
	fragShadowPos = shadow_mv_matrix * modelMatrix * orient * vertPosition;
	fragShadowUV[0] = transformToShadowMap(0, fragShadowPos);
	fragShadowUV[1] = transformToShadowMap(1, fragShadowPos);
	fragShadowUV[2] = transformToShadowMap(2, fragShadowPos);
	fragShadowUV[3] = transformToShadowMap(3, fragShadowPos);
 #endif

#ifndef HAS_GEOMETRY_SHADER
    // Setup stuff for normal maps
    if (FLAG_NORMAL_MAP) {
        vec3 t = normalize(mat3(modelViewMatrix) * mat3(orient) * vertTangent.xyz);
        vec3 b = cross(normal, t) * vertTangent.w;
        fragTangentMatrix = mat3(t, b, normal);
    }
#endif

 #ifdef FLAG_ENV_MAP
 // Environment mapping reflection vector.
	fragEnvReflect = reflect(normalize(position.xyz), normal);
	fragEnvReflect = vec3(envMatrix * vec4(fragEnvReflect, 0.0));
 #endif
 #ifdef FLAG_FOG
	fragFogDist = clamp((gl_Position.z - fogStart) * 0.75 * fogScale, 0.0, 1.0);
 #endif
 #ifdef FLAG_CLIP
   float clip_dist = 0.0;
	if(use_clip_plane == 1) clip_dist = dot(normalize((modelMatrix * orient * vertex).xyz - clip_position), clip_normal);
  #ifdef FLAG_SHADOW_MAP
   geoClipDistance = clip_dist;
  #else
   fragClipDistance = clip_dist;
  #endif
 #endif
 #ifndef FLAG_SHADOW_MAP
	fragPosition = position;
	fragNormal = normal;
	fragTexCoord = texCoord;
 #else
	geoNormal = normal;
	geoTexCoord = texCoord;
 #endif
}
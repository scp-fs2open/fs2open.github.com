in vec3 beamVec;
in vec3 lightPosition;
out vec4 fragOut0;

uniform sampler2D ColorBuffer;
uniform sampler2D NormalBuffer;
uniform sampler2D PositionBuffer;
uniform sampler2D SpecBuffer;

uniform float invScreenWidth;
uniform float invScreenHeight;

layout (std140) uniform lightData {
	vec3 diffuseLightColor;
	float coneAngle;

	vec3 specLightColor;
	float coneInnerAngle;

	vec3 coneDir;
	bool dualCone;

	vec3 scale;
	float lightRadius;

	int lightType;
};

#define SPEC_INTENSITY_POINT			5.3 // Point light
#define SPEC_INTENSITY_DIRECTIONAL		3.0 // Directional light
#define SPECULAR_FACTOR				1.75
#define SPECULAR_ALPHA					0.1
#define SPEC_FACTOR_NO_SPEC_MAP		0.6
#define ENV_ALPHA_FACTOR				0.3
#define GLOW_MAP_INTENSITY				1.5
#define AMBIENT_LIGHT_BOOST			1.0
vec3 FresnelSchlick(vec3 specColor, vec3 light, vec3 halfVec)
{
	return specColor + (vec3(1.0) - specColor) * pow(1.0 - clamp(dot(light, halfVec), 0.0, 1.0), 5.0);
}
vec3 SpecularBlinnPhong(vec3 specColor, vec3 light, vec3 normal, vec3 halfVec, float specPower, float fresnel, float dotNL)
{
	return mix(specColor, FresnelSchlick(specColor, light, halfVec), fresnel) * ((specPower + 2.0) / 8.0 ) * pow(clamp(dot(normal, halfVec), 0.0, 1.0), specPower) * dotNL;
}
vec3 SpecularGGX(vec3 specColor, vec3 light, vec3 normal, vec3 halfVec, vec3 view, float gloss, float dotNL)
{
	float roughness = clamp(1.0f - gloss, 0.0f, 1.0f);
	float alpha = roughness * roughness;

	float dotNH = clamp(dot(normal, halfVec), 0.0f, 1.0f);
	float dotNV = clamp(dot(normal, view), 0.0f, 1.0f);

	float alphaSqr = alpha * alpha;
	float pi = 3.14159f;
	float denom = dotNH * dotNH * (alphaSqr - 1.0f) + 1.0f;
	float distribution = alphaSqr / (pi * denom * denom);

	vec3 fresnel = FresnelSchlick(specColor, light, halfVec);
	
	float alphaPrime = roughness + 1.0f;
	float k = alphaPrime * alphaPrime / 8.0f;
	float g1vNL = 1.0f / (dotNL * (1.0f - k) + k);
	float g1vNV = 1.0f / (dotNV * (1.0f - k) + k);
	float visibility = g1vNL * g1vNV;

	return distribution * fresnel * visibility * dotNL;
}
vec4 SpecularLegacy(vec4 specColor, vec3 normal, vec3 halfVec, float specPower)
{
	return specColor * pow(clamp(dot(normal, halfVec), 0.0, 1.0), specPower);
}
void main()
{
	vec2 screenPos = gl_FragCoord.xy * vec2(invScreenWidth, invScreenHeight);
	vec3 position = texture(PositionBuffer, screenPos).xyz;

	if(abs(dot(position, position)) < 0.1)
		discard;
	vec3 lightDir = lightPosition - position.xyz;
	float dist = length(lightDir);
	if(dist > lightRadius && lightType != 1)
		discard;
	vec3 color = texture(ColorBuffer, screenPos).rgb;
	vec4 normal = texture(NormalBuffer, screenPos);
	vec4 specColor = texture(SpecBuffer, screenPos);
	float gloss = normal.a;
	float fresnel = specColor.a;
	vec3 eyeDir = normalize(-position);

	if(lightType == 1)
	{
		float beamLength = length(beamVec);
		vec3 beamDir = beamVec / beamLength;
		// Get nearest point on line
		float neardist = clamp(dot(lightDir, beamDir), 0.0, beamLength);
		// Move back from the endpoint of the beam along the beam by the distance we calculated
		vec3 nearest = lightPosition - beamDir * neardist;
		lightDir = nearest - position.xyz;
		dist = length(lightDir);
		if(dist > lightRadius)
			discard;
	}

	float attenuation = 1.0 - clamp(dist / lightRadius, 0.0, 1.0);

	if(lightType == 2)
	{
		float coneDot = dot(normalize(-lightDir), coneDir);
		if(dualCone) {
			if(abs(coneDot) < coneAngle)
				discard;
			else
				attenuation *= smoothstep(coneAngle, coneInnerAngle, abs(coneDot));
		} else {
			if(coneDot < coneAngle)
				discard;
			else
				attenuation *= smoothstep(coneAngle, coneInnerAngle, coneDot);
		}
	}

	lightDir /= dist;
	vec3 halfVec = normalize(lightDir + eyeDir);
	float NdotL = clamp(dot(normal.xyz, lightDir), 0.0, 1.0);
	vec4 fragmentColor = vec4(color * (diffuseLightColor * NdotL * attenuation), 1.0);
	fragmentColor.rgb += SpecularGGX(specColor.rgb, lightDir, normal.xyz, halfVec, eyeDir, gloss, NdotL).rgb * specLightColor * attenuation;
	fragOut0 = max(fragmentColor, vec4(0.0));
}

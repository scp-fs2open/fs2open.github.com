//? #version 150

// This shader performs downsampling on a texture,
// as taken from Call Of Duty method, presented at ACM Siggraph 2014.
// This particular method was customly designed to eliminate
// "pulsating artifacts and temporal stability issues".

// Remember to add bilinear minification filter for this texture!
// Remember to use a floating-point texture format (for HDR)!
// Remember to use edge clamping for this texture!

uniform sampler2D srcTexture;

layout (std140) uniform genericData {
	float xSize;
	float ySize;
	float mip;
	float UNUSED; // We don't need this for downsampling (it's padding anyway)
};

in vec4 fragTexCoord;

out vec4 fragOut0;

void main()
{
    float x = xSize;
    float y = ySize;

    // Take 13 samples around current texel:
    // a - b - c
    // - j - k -
    // d - e - f
    // - l - m -
    // g - h - i
    // === ('e' is the current texel) ===
    vec3 a = textureLod(srcTexture, vec2(fragTexCoord.x - 2*x, fragTexCoord.y + 2*y), mip).rgb;
    vec3 b = textureLod(srcTexture, vec2(fragTexCoord.x,       fragTexCoord.y + 2*y), mip).rgb;
    vec3 c = textureLod(srcTexture, vec2(fragTexCoord.x + 2*x, fragTexCoord.y + 2*y), mip).rgb;

    vec3 d = textureLod(srcTexture, vec2(fragTexCoord.x - 2*x, fragTexCoord.y), mip).rgb;
    vec3 e = textureLod(srcTexture, vec2(fragTexCoord.x,       fragTexCoord.y), mip).rgb;
    vec3 f = textureLod(srcTexture, vec2(fragTexCoord.x + 2*x, fragTexCoord.y), mip).rgb;

    vec3 g = textureLod(srcTexture, vec2(fragTexCoord.x - 2*x, fragTexCoord.y - 2*y), mip).rgb;
    vec3 h = textureLod(srcTexture, vec2(fragTexCoord.x,       fragTexCoord.y - 2*y), mip).rgb;
    vec3 i = textureLod(srcTexture, vec2(fragTexCoord.x + 2*x, fragTexCoord.y - 2*y), mip).rgb;

    vec3 j = textureLod(srcTexture, vec2(fragTexCoord.x - x, fragTexCoord.y + y), mip).rgb;
    vec3 k = textureLod(srcTexture, vec2(fragTexCoord.x + x, fragTexCoord.y + y), mip).rgb;
    vec3 l = textureLod(srcTexture, vec2(fragTexCoord.x - x, fragTexCoord.y - y), mip).rgb;
    vec3 m = textureLod(srcTexture, vec2(fragTexCoord.x + x, fragTexCoord.y - y), mip).rgb;

    // Apply weighted distribution:
    // 0.5 + 0.125 + 0.125 + 0.125 + 0.125 = 1
    // a,b,d,e * 0.125
    // b,c,e,f * 0.125
    // d,e,g,h * 0.125
    // e,f,h,i * 0.125
    // j,k,l,m * 0.5
    // This shows 5 square areas that are being sampled. But some of them overlap,
    // so to have an energy preserving downsample we need to make some adjustments.
    // The weights are the distributed, so that the sum of j,k,l,m (e.g.)
    // contribute 0.5 to the final color output. The code below is written
    // to effectively yield this sum. We get:
    // 0.125*5 + 0.03125*4 + 0.0625*4 = 1
    vec3 downsample = e*0.125;
    downsample += (a+c+g+i)*0.03125;
    downsample += (b+d+f+h)*0.0625;
    downsample += (j+k+l+m)*0.125;

	fragOut0.rgb = downsample;
}
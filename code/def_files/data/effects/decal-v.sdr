
in vec4 vertPosition;
in mat4 vertModelMatrix;

out flat mat4 invModelMatrix;
out flat vec3 decalDirection;
out flat float normal_angle_cutoff;
out flat float angle_fade_start;
out flat float alpha_scale;

layout (std140) uniform decalGlobalData {
	mat4 viewMatrix;
	mat4 projMatrix;
	mat4 invViewMatrix;
	mat4 invProjMatrix;

	vec3 ambientLight;
	float pad0;

	vec2 viewportSize;
};

layout (std140) uniform decalInfoData {
	int diffuse_index;
	int glow_index;
	int normal_index;
	int diffuse_blend_mode;

	int glow_blend_mode;
};

void main() {
    normal_angle_cutoff = vertModelMatrix[0][3];
    angle_fade_start = vertModelMatrix[1][3];
    alpha_scale = vertModelMatrix[2][3];

    mat4 modelMatrix = vertModelMatrix;
    modelMatrix[0][3] = 0.0;
    modelMatrix[1][3] = 0.0;
    modelMatrix[2][3] = 0.0;

    invModelMatrix = inverse(modelMatrix);
    decalDirection = mat3(viewMatrix) * vec3(modelMatrix[0][2], modelMatrix[1][2], modelMatrix[2][2]);
	gl_Position = projMatrix * viewMatrix * modelMatrix * vertPosition;
}
